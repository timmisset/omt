{
  parserClass="org.intellij.sdk.language.parser.OMTParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="OMT"
  psiImplClassSuffix="Impl"
  psiPackage="com.misset.opp.omt.psi"
  psiImplPackage="com.misset.opp.omt.psi.impl"

  elementTypeHolderClass="com.misset.opp.omt.psi.OMTTypes"
  elementTypeClass="com.misset.opp.omt.psi.OMTElementType"
  tokenTypeClass="com.misset.opp.omt.psi.OMTTokenType"

  psiImplUtilClass="com.misset.opp.omt.psi.util.PsiImplUtil"

}

// the end of the file (or the entire file) can be a (set of) comment(s)
OMTFile ::= leading? block? trailing?

start ::=                                   START_TOKEN leading?
// sometimes the comments following a block are indented wrong but this is not forbidden
// however, to correctly close the indented block preceeding the comment, the comment should
// be encapsulated within this end statement. This will be the leading block and should not be considered
// an actual comment on the item closed by this end block
end ::=                                     trailing? END_TOKEN leading?
indentToken ::=                             INDENT | INDENT2
dedentToken ::=                             DEDENT | DEDENT2
// a block is a yaml block consisting of a propertyLabel (key): and a sequence, a map or a scalar
block ::=                                   leading? (blockEntry+ | (indentToken blockEntry+ dedentToken)) trailing?
blockEntry ::=                              leading? specificBlock | (leading? propertyLabel trailing? ((block | sequence | scalar) | (indentToken (block | sequence | scalar) dedentToken))) {
                                            mixin="com.misset.opp.omt.psi.impl.OMTAnnotationHolderImpl"
                                            implements="com.misset.opp.omt.psi.support.OMTAnnotationHolder"
                                            methods=[ getName getLabel ]
}
propertyLabel ::=                           PROPERTY { methods=[getPropertyLabelName]}

// a scalar single statement (multiline optional) following a property label
scalar ::=                                  comment? scalarValue
scalarValue ::=                             (start (parameterWithType | variableAssignment | block | query | script) end) | nakedString | EMPTY_ENTRY

// a sequence must start with a newline followed by items, lead by their bullet (-)
// indentation is allowed but not required. If indentToken, it must 
sequence ::=                                trailing? sequenceItem+
sequenceItem ::=                            leading? SEQUENCE_BULLET (scalarValue | block)

// SPECIFIC BLOCKS
specificBlock ::=                          importBlock | exportBlock | prefixBlock | queriesBlock | commandsBlock | modelBlock | moduleBlock {
                                                    methods=[getName]
                                                }

// sequences
exportBlock ::=                             EXPORT_START trailing? memberList?
memberList ::=                              memberListItem+ | (indentToken memberListItem+ dedentToken)
memberListItem ::=                          (leading? SEQUENCE_BULLET member)
member ::=                                  start OPERATOR end {
                                                       mixin="com.misset.opp.omt.psi.impl.OMTMemberNamedElementImpl"
                                                       implements="com.misset.opp.omt.psi.named.OMTMemberNamedElement"
                                                       methods=[getName setName getNameIdentifier]
                                                     }

// scalars
queriesBlock::=                             QUERY_BLOCK_START (start PIPE trailing? defineQueryStatement* end)? {
                                                                implements="com.misset.opp.omt.psi.support.OMTDefinedBlock"
                                                                methods=[getStatements]
                                                                }
commandsBlock::=                            COMMAND_BLOCK_START (start PIPE trailing? defineCommandStatement* end)? {
                                                                implements="com.misset.opp.omt.psi.support.OMTDefinedBlock"
                                                                methods=[getStatements]
                                                                }

moduleBlock::=                              leading? MODULE_NAME_START moduleName trailing?
moduleName::=                               NAME | (start OPERATOR end)

// maps
importBlock ::=                             leading? IMPORT_START trailing? (indentToken import* dedentToken)?
import ::=                                  leading? importSource trailing? memberList?
importSource ::=                            importLocation (start EMPTY_ARRAY end)? {
                                                                           mixin="com.misset.opp.omt.psi.impl.OMTImportNamedElementImpl"
                                                                           implements="com.misset.opp.omt.psi.named.OMTImportNamedElement"
                                                                           methods=[getName setName getNameIdentifier]
                                                                         }
importLocation ::=                          PROPERTY | IMPORT_PATH

prefixBlock::=                              PREFIX_BLOCK_START trailing? (prefix+ | (indentToken prefix+ dedentToken))?
prefix::=                                   leading? namespacePrefix namespaceIri trailing? { methods=[getName] }
namespacePrefix::=                          PROPERTY | (NAMESPACE COLON)            {
                                                                                         mixin="com.misset.opp.omt.psi.impl.OMTNamespacePrefixNamedElementImpl"
                                                                                         implements="com.misset.opp.omt.psi.named.OMTNamespacePrefixNamedElement"
                                                                                         methods=[getName setName getNameIdentifier]
                                                                                   }
namespaceIri ::=                            start IRI end                          { methods = [getName getNamespace] }
curieElement ::=                            namespacePrefix NAMESPACE_MEMBER       { methods = [getPrefix isDefinedByPrefix getAsResource getPrefixName]}
curieConstantElement ::=                    CURIE_CONSTANT_ELEMENT_PREFIX curieElement { extends=queryStep methods=[resolveToResource ]}

modelBlock::=                               MODEL_BLOCK_START trailing? (indentToken modelItemBlock* dedentToken)?
modelItemBlock::=                           leading? modelItemLabel trailing? block?
modelItemLabel::=                           propertyLabel modelItemTypeElement  {
                                                                                 mixin="com.misset.opp.omt.psi.impl.OMTMemberNamedElementImpl"
                                                                                 implements="com.misset.opp.omt.psi.named.OMTMemberNamedElement"
                                                                                 methods=[getName setName getNameIdentifier]
                                                                                 }
modelItemTypeElement ::=                    MODEL_ITEM_TYPE

// COMMENTS
// Can occur on empty lines (JavaDocs and End Of Line Comments) or at the end of lines (End of Line Comments)
jdComment ::=                               JAVADOCS_START jdContent* JAVADOCS_END END_TOKEN?
jdContent ::=                               JAVADOCS_CONTENT+ | START_TOKEN | END_TOKEN | parameterAnnotation
parameterAnnotation ::=                     ANNOTATE_PARAMETER parameterWithType
comment ::=                                 END_OF_LINE_COMMENT | jdComment
eolComment ::=                              END_OF_LINE_COMMENT
// trailing is everything at the end of the current line and before NEW_LINE.
// It can be simply the NEW_LINE or an eolComment followed by a NEW_LINE
trailing ::=                                eolComment+
// leading? is everything before a block or item. Assuming that comments are written above the code they are
// commenting on, this should help to relate the comment to the code
leading ::=                                 comment+

// ////////////////////////////////////////////////////
// ODT
// ////////////////////////////////////////////////////
constant_value ::=                          STRING | INTEGER | DECIMAL | NULL | BOOLEAN | TYPED_VALUE
// a variable is declared using VAR and one or more variable names. Optionally an assignment is added
// the assignment is deconstructed to the entire set of variables that is declared. i.e. VAR $a, $b = [1, 2]. $a == 1, $b == 2
// or can be VAR $a = 1, $b = 2 etc. todo: check if this is actually true
declareVariable ::=                         DECLARE_VAR (variableAssignment | variable) (COMMA (variableAssignment | variable))*
// a variable assignment is $var = 1234
variableAssignment::=                       variable (COMMA variable)* EQUALS variableValue
// a variable can be assigned with anything
variableValue ::=                           query | commandCall | constant_value

// a parameter type is used to typecast input parameters in OMT model items
variable ::=                                VARIABLE_NAME | ignoredVariable | globalVariable {
                                                                                 mixin="com.misset.opp.omt.psi.impl.OMTVariableNamedElementImpl"
                                                                                 implements="com.misset.opp.omt.psi.named.OMTVariableNamedElement"
                                                                                 methods=[getName setName isDeclaredVariable isIgnoredVariable isGlobalVariable getNameIdentifier getType]
                                                                               }
// the lexer should determine if operating in a state of declare and provide a difference response for the discovered variable name
globalVariable ::=                          GLOBAL_VARIABLE_NAME
ignoredVariable ::=                         IGNORE_VARIABLE_NAME
parameterWithType ::=                       variable PARENTHESES_OPEN (parameterType | OPERATOR) PARENTHESES_CLOSE { methods=[ getType ]}

parameterType ::=                           IRI | (namespacePrefix NAMESPACE_MEMBER) {
    methods = [isDefinedByPrefix getAsResource]
}

// SCRIPT
// an ODT (script) block is always started by a Yaml scalar node
// it can start on the same line and be restricted to that line (not required) or it can be started by the
// literal indentifier, the "|" in Yaml. In which case it should be followed by an empty line and and 
script ::=                                  PIPE trailing? (scriptLine+ | query)?
commandBlock ::=                            CURLY_OPEN trailing? (script | scriptLine+)? CURLY_CLOSED
// a scriptline is contained by the addition of the semicolon at the end
scriptLine ::=                              leading? ((scriptContent SEMICOLON) | commandBlock | logicalBlock) trailing?
// the contents of a script, contained by a scriptLine or Scalar
scriptContent ::=                           declareVariable | variableAssignment | definePrefix | commandCall | assignmentStatement |
                                            addToCollection | removeFromCollection | logicalBlock | returnStatement | query

// DEFINED MEMBERS
// ODT Members are Operator and Commands, both can be created by a DEFINE statement
// there is also a collection of BuiltIn operators and commands which are included
// and finally, all runnable OMT modelitems are available as Commands (Activity, Procedure) or Operator (StandAloneQuery)
// only the Defined members are part of set of the grammar parser
defineName ::=                              NAME | OPERATOR    {
                                                           mixin="com.misset.opp.omt.psi.impl.OMTMemberNamedElementImpl"
                                                           implements="com.misset.opp.omt.psi.named.OMTMemberNamedElement"
                                                           methods=[getName setName getNameIdentifier]
                                                         }
defineQueryStatement ::=                    leading? DEFINE_START DEFINE_QUERY defineName defineParam? LAMBDA query SEMICOLON trailing? {
                                                            implements="com.misset.opp.omt.psi.support.OMTDefinedStatement"
                                                            extends="com.misset.opp.omt.psi.impl.OMTDefinedStatementImpl"
                                            }
defineCommandStatement ::=                  leading? DEFINE_START DEFINE_COMMAND defineName defineParam? LAMBDA commandBlock trailing? {
                                                          implements="com.misset.opp.omt.psi.support.OMTDefinedStatement"
                                                          extends="com.misset.opp.omt.psi.impl.OMTDefinedStatementImpl"
                                            }
definePrefix ::=                            leading? PREFIX_DEFINE_START prefix trailing?
defineParam ::=                             PARENTHESES_OPEN (variable (COMMA variable)*)? PARENTHESES_CLOSE

// MEMBER CALLS
// A call to either an Operator or a Command. In case of the latter, it's always prefixed with @
operatorCall ::=                            OPERATOR flagSignature? signature? {
                                                             mixin="com.misset.opp.omt.psi.impl.OMTCallImpl"
                                                             implements="com.misset.opp.omt.psi.support.OMTCall"
                                                             methods=[getName setName getNameIdentifier canCallOperator canCallCommand]
                                                           }
commandCall ::=                             COMMAND flagSignature? signature?
                                                            {
                                                             mixin="com.misset.opp.omt.psi.impl.OMTCallImpl"
                                                             implements="com.misset.opp.omt.psi.support.OMTCall"
                                                             methods=[getName setName getNameIdentifier canCallOperator canCallCommand]
                                                           }
signature ::=                               PARENTHESES_OPEN (signatureArgument (COMMA signatureArgument)*)? PARENTHESES_CLOSE {
                                                             methods=[numberOfParameters]
}
signatureArgument ::=                       (query | commandBlock | commandCall | operatorCall)
flagSignature ::=                           FLAG

logicalBlock ::=                            ifBlock commandBlock trailing? (ELSE_OPERATOR ifBlock commandBlock)* elseBlock?
ifBlock ::=                                 IF_OPERATOR (query | (PARENTHESES_OPEN query PARENTHESES_CLOSE))
elseBlock ::=                               ELSE_OPERATOR commandBlock
returnStatement ::=                         RETURN_OPERATOR (query | commandCall)?
chooseBlock ::=                             CHOOSE_OPERATOR trailing?
                                                leading? (WHEN_OPERATOR booleanStatement LAMBDA query)+
                                                leading? (OTHERWISE_OPERATOR LAMBDA query)?
                                                END_OPERATOR trailing?

// QUERIES
// A query is the superclass of different variants of a query.
// it is used to break the array into meaningful subsections based on detected tokens
// for example, the queryArray will break the array into subsections by the delimiter '|' for the array
//
// with regular bnf -> psi generation, the query would encapsulate the queryPath, ... items
// by using the extension mechanism, it only becomes their superclass
query     ::=                               queryPath | queryArray | booleanStatement | equationStatement
                                            { methods = [
                                            resolveToResource
                                            filter
                                            isBooleanType
                                            getDefinedName ]  }
// The queryPath is the base, eventually all queries will descent into a queryPath even for single steps
// For example, a queryArray can consist of 'a' | 'b' in which both 'a' and 'b' are encapsulated
// into a queryPath and subsequently a queryStep
// The queryPath is constructed using the Forward slash as delimiter a contains as many steps as possible
queryPath ::=                               (FORWARD_SLASH? queryStep)+ { extends=query methods=[resolveToResource] }
// The queryArray uses the PIPE symbol to split the array where both can be resolved separately
queryArray ::=                              query trailing? (PIPE trailing? query)+ { extends=query methods=[resolveToResource] }
// A booleanStatement works the same as the queryArray but using a BOOLEAN_OPERATOR
// When resolving, it should be checked that all parts of the query are resolved to boolean types
booleanStatement ::=                        query trailing? (BOOLEAN_OPERATOR trailing? query)+ { extends=query methods=[resolveToResource filter] }
// The equationStatement uses a conditional operator (like ==, >=, <=) to split the statement
// it only allows a left-hand and right-hand query, both should be resolved to boolean type
equationStatement ::=                       query trailing? CONDITIONAL_OPERATOR trailing? query { extends=query methods=[resolveToResource filter getOpposite] }
// The negated statement detects the usage of the NOT operator
// This can be used as '.. / NOT IN (...)' OR '.. / NOT'. In the former the negation is based on the subsequent
// query whereas the latter it's based on the preceding query
// NOTE: for some reason the 2 options need to be stated separately for the compiler to make sense of it
// creation of a single negatedStatement with both options is not working
// the leading statement is preferred
negatedStep ::=                             NOT_OPERATOR trailing? query { extends=query }

// a queryStep is anything that is acceptable between the / ... / FORWARD_SLASH delimiters in the query or
// that add some additional behavior to the step (like a filter)
// TODO: it might be worthwhile to consider appending the filter to the queryStep instead of making it a separate step
queryStep ::=                               (queryReverseStep |
                                            constant_value | variable | curieConstantElement | curieElement |
                                            operatorCall | IRI | DOT | OWLPROPERTY | interpolatedString |
                                            queryFilter | chooseBlock | negatedStep | NOT_OPERATOR |
                                            subQuery |
                                            trailing | leading) stepDecorator?
                                            { methods = [ resolveToResource ]}
queryReverseStep ::=                        CARET queryStep { methods = [ resolveToResource ] extends=queryStep}
stepDecorator ::=                           ASTERIX | PLUS
// a query filter is contained by brackets [] and should resolve to boolean
queryFilter ::=                             BRACKET_OPEN trailing? query trailing? BRACKET_CLOSED { extends=queryStep }

// the subQuery encapsulated a query by PARENTHESES, it can be resolved as-is, for example in an equation
// or be cast to a traverse-like behavior using the ASTERIX and PLUS appendixes
subQuery ::=                                PARENTHESES_OPEN trailing? query trailing? PARENTHESES_CLOSE { extends=queryStep methods=[resolveToResource] }

// the assignment statement is used to assign a value to a left-hand target, like a variable or a resolved resource iri
// addTo and removeFrom are similar which will add/remove the item to/from the collection
// these are not used defined query statement but in command blocks
assignmentStatement ::=                     query trailing? EQUALS trailing? (query | commandCall)
addToCollection ::=                         query trailing? ADD trailing? (query | commandCall)
removeFromCollection ::=                    query trailing? REMOVE trailing? (query | commandCall)

// interpolation
interpolationTemplate ::=                   TEMPLATE_OPEN query TEMPLATE_CLOSED
interpolatedString ::=                      BACKTICK (interpolationTemplate | STRING)* BACKTICK { extends=queryStep }

// string / interpolated string without encapsulation of ' or `
nakedString ::=                             start (queryStep | COMMA | EQUALS | SEQUENCE_BULLET | interpolationTemplate)+ end
